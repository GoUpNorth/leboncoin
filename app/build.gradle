plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

android {
    compileSdk 32

    defaultConfig {
        applicationId "com.goupnorth.leboncoin"
        minSdk 24
        targetSdk 32
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11
        freeCompilerArgs = ["-opt-in=kotlinx.coroutines.ExperimentalCoroutinesApi"]
    }

    buildFeatures {
        viewBinding true
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

apply from: file("../dependencies.gradle")

dependencies {

    implementation "androidx.core:core-ktx:$androidx_core_version"
    implementation "androidx.appcompat:appcompat:$appcompat_version"
    implementation "com.google.android.material:material:$material_version"

    // Dependency injection
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version" // migrate to ksp when supported
    implementation "androidx.fragment:fragment-ktx:$fragment_version" // for the viewModels delegate

    // Presentation layer
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"

    // Coroutines + Flows
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    // http requests
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit_version"
    implementation "com.squareup.okhttp3:logging-interceptor:4.10.0"

    // image loading + image offline caching
    implementation "com.github.bumptech.glide:glide:$glide_version"
    kapt "com.github.bumptech.glide:compiler:$glide_version" // migrate to ksp when supported
    implementation("com.github.bumptech.glide:okhttp3-integration:$glide_version") {
        exclude group: "com.squareup.okhttp3", module: "okhttp"
    }

    // LeakCanary only for debug builds
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leak_canary_version"

    // Paging
    implementation "androidx.paging:paging-runtime:$paging_version"

    // Testing
    testImplementation "org.robolectric:robolectric:$robolectric_version"
    testImplementation "app.cash.turbine:turbine:$turbine_version" // for kotlin flow testing
    testImplementation "io.mockk:mockk:$mockk_version"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_version"
    testImplementation "junit:junit:$junit_version"

    implementation project(":domain")
    implementation project(":data")
}